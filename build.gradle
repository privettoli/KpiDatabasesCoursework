import static org.codehaus.groovy.runtime.IOGroovyMethods.withCloseable

group 'ua.kpi.databases'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        mavenCentral()
    }
    ['dependencies.versions', 'src/main/resources/db/settings'].forEach { fileName ->
        logger.debug("Loading $fileName")
        def props = new Properties()
        withCloseable(new FileInputStream("$project.rootDir/${fileName}.properties")) {
            props.load(it)
            logger.debug("$fileName loaded. Props: ${props.entrySet()}")
        }
        props.each { prop ->
            project.extensions.extraProperties.set(prop.key as String, prop.value)
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$KotlinVersion"
        classpath "org.liquibase:liquibase-gradle-plugin:$LiquibaseVersion"
        classpath "mysql:mysql-connector-java:$MySqlConnectorVersion"
    }
}

apply plugin: 'org.liquibase.gradle'
apply plugin: 'kotlin'
apply plugin: 'application'

sourceCompatibility = 1.8
mainClassName = 'ua.kpi.databases.coursework.ApplicationKt'
defaultTasks 'run'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.slf4j:slf4j-simple:$Slf4jLoggerVersion"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$KotlinVersion"
    compile "nl.komponents.kovenant:kovenant:$KovenantVersion"
    compile "nl.komponents.kovenant:kovenant-jfx:$KovenantVersion"
    compile "nl.komponents.kovenant:kovenant-ui:$KovenantVersion"
    compile "com.github.andrewoma.kwery:core:$KweryVersion"
    compile "mysql:mysql-connector-java:$MySqlConnectorVersion"
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.10"
}

liquibase {
    activities {
        main {
            changeLogFile 'src/main/resources/db/list.xml'
            url "$url"
            username "$username"
            password "$password"
        }
    }
}